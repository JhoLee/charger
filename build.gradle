subprojects {
    version = '0.0.1-SNAPSHOT'
    group = 'io.marauder'

    plugins.withType(JavaPlugin) {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'

        plugins.withType(MavenPlugin) {
            javadoc {
                // link to JRE6 documentation
                options.author true
                options.links 'http://download.oracle.com/javase/8/docs/api/'
                options.links 'http://vertx.io/docs/apidocs/'
            }
            
            // package javadoc into a jar file
            task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
                from javadoc.destinationDir
                classifier = 'javadoc'
            }

            // package source into a jar file
            task packageSources(type: Jar) {
                from sourceSets.main.allSource
                classifier = 'sources'
            }

            // define artifacts for upload
            artifacts {
                archives jar
                archives packageJavadoc
                archives packageSources
            }
            
        }
    }

    ext {
        kotlinVersion = '3.11.0'
    }

    plugins.withType(MavenPlugin) {

        uploadArchives.repositories.mavenDeployer {
            // upload to sonatype OSS
            repository(url: "https://geocode.igd.fraunhofer.de:8081/artifactory/libs-release-local") {
                authentication(userName: rootProject.hasProperty('artifactory_user') ? "$artifactory_user" : "$System.env.artifactory_user",
                    password: rootProject.hasProperty('artifactory_password') ? "$artifactory_password" : "$System.env.artifactory_password")
            }
            snapshotRepository(url: "https://geocode.igd.fraunhofer.de:8081/artifactory/libs-snapshot-local") {
                authentication(userName: this.hasProperty('artifactory_user') ? "$artifactory_user" : "$System.env.artifactory_user",
                    password: this.hasProperty('artifactory_password') ? "$artifactory_password" : "$System.env.artifactory_password")
            }

            // ensure correct artifact ID
            pom.artifactId = project.name

            // pom file details
            pom.project {
                name 'supercharger'
                packaging 'jar'
                description 'A Collection of Vector Tile Tools including the supercharged Encoder!'
                url 'http://marauder.io/supercharger'

                scm {
                    url 'scm:git:git://geocode.igd.fraunhofer.de/marauder/supercharger.git'
                    connection 'scm:git:git://geocode.igd.fraunhofer.de/marauder/supercharger.git'
                    developerConnection 'scm:git:git://geocode.igd.fraunhofer.de/marauder/supercharger.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'florian-zouhar'
                        name 'Florian Zouhar'
                        email 'florian.zouhar@igd.fraunhofer.de'
                    }
                }
            }
        }
    }

}
